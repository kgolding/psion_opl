INCLUDE "System.oxh"
INCLUDE "Const.oph"
CONST KUidAppFrame& = &100041BD

PROC Main:
	GLOBAL width%, height%, fname$(255)
	GLOBAL fontdescent%, fontheight%
	GLOBAL i%, maxchildren&, totalitems&
	LOCAL i&(48), d%
		
	dINIT "What to do?"
	dBUTTONS "Draw all", %d, "Create sample data", %s, "Cancel", -27
	d% = DIALOG
	IF d% = %d
		fname$ = OPENFILEDIALOG$:(fname$, 0, 0, KUidAppFrame&)
		OPEN """" + fname$ + """ SELECT * FROM items", A, id&, parent&, title$
		width% = gWIDTH
		height% = gHEIGHT
		gFONT KFontArialNormal8&
		gINFO32 i&()
		fontheight% = i&(3) + i&(4)		REM Set global total height off font
		fontdescent% = i&(4)					REM Set global descent off font
		DrawChildren:(width% / 2, height% / 2, 22, 359 + 22, &1)
	ELSEIF d% = %s
		fname$ = CREATEFILEDIALOG$:(fname$)
		IF fname$ <> ""
			CreateSampleData:
		ENDIF
	ELSE
		STOP
	ENDIF
	
	BUSY "Done! Press any key to exit"
	GET
ENDP

PROC CreateSampleData:
	LOCAL p&, c%
	
	REM Create file here
	TRAP DELETE fname$
	CREATE """" + fname$ + """ FIELDS id,type(12),data(240) TO data", A, id&, type$, data$
	INSERT
	A.id& = 0
	A.type$ = "ProgVer"
	A.data$ = "Test Data"
	PUT
	CLOSE
	
	CREATE """" + fname$ + """ FIELDS id,parent,title(40) TO items", A, id&, parent&, title$

	REM Add root item
	INSERT
	A.id& = &1
	A.parent& = &0
	A.title$ = "Test root item"
	PUT
	
	dINIT "Create Sample data"
	dLONG totalitems&, "Total Items", 1, 1000
	dLONG maxchildren&, "Max children per item", 1, 10
	DIALOG
	
	i% = 2
	p& = &1
	
	AddChildren:(p&, INT(RND * maxchildren&))
	
ENDP

PROC AddChildren:(id&, qty&)
	LOCAL c%
	c% = qty&
	WHILE c% > 0
		INSERT
		A.id& = i%
		A.parent& = id&
		A.title$ = "Item number " + GEN$(i%, 10)
		PUT
		i%++
		c%--
		AddChildren:(A.id&, INT(RND * maxchildren&))
	ENDWH	
ENDP

PROC DrawChildren:(x%, y%, angle1%, angle2%, id&)
	LOCAL ang%, step%, gotofirst%, numChildren%
	LOCAL temp&
	
	Goto_id%:(id&, -1)
	DrawText:(x%, y%, A.title$)
	
	numChildren% = numberChildren%:(id&)
	IF numChildren% = 0
		RETURN
	ENDIF
	IF numChildren% = 1
		ang% = (angle1% + angle2%) / 2
	ELSE
		ang% = angle1%
	ENDIF
	step% = (angle2% - angle1%) / numChildren%
	gotofirst% = -1
	WHILE Goto_parent%:(id&, gotofirst%)
		gAT x%, y%
		gLINEBY SIN(RAD(ang%)) * 80, COS(RAD(ang%)) * 50
		
		temp& = A.id&
		DrawChildren:(gX, gY, ang% - (step% / 2), ang% + (step% / 2), A.id&)
		
		ang% = ang% + step%
		Goto_id%:(temp&, -1)
		gotofirst% = 0
		NEXT
	ENDWH
	
ENDP

PROC DrawText:(x%, y%, s$)
	LOCAL xs%, xa%, ya%
	
	xs% = gTWIDTH(s$)
	xa% = x% - (xs% / 2)
	ya% = y% + (fontheight% / 2) - fontdescent%

	gAT xa%, ya%	
	gPRINTB s$, xs%, 3
	
ENDP

PROC Goto_parent%:(id&, first%)
	USE A
	IF first%
		FIRST
	ENDIF
	WHILE NOT EOF
		IF A.parent& = id&
			RETURN -1
		ENDIF
		NEXT
	ENDWH
	RETURN 0
ENDP

PROC NumberChildren%:(id&)
	LOCAL c%
	REM Returns number of childen for given item id
	
	OPEN """" + fname$ + """ SELECT parent FROM items WHERE parent=" + GEN$(id&, 20), C, parent&
	c% = COUNT
	CLOSE
	RETURN c%
ENDP

PROC Goto_id%:(id&, first%)
	USE A
	IF first%
		FIRST
	ENDIF
	WHILE NOT EOF
		IF A.id& = id&
			RETURN -1
		ENDIF
		NEXT
	ENDWH
	RETURN 0
ENDP
