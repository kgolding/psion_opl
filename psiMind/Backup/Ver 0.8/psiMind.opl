REM psiMind
REM
REM This file was created by AppFrame on Mon 30 Apr 2001 21:11:12
REM AppFrame for the Psion 5, 5mx, 7 and Netbook, by K.Golding (C) 1999-2000
REM Create by Version dated 3/7/00
	
INCLUDE "Const.oph"
INCLUDE "System.oxh"
INCLUDE "Printer.oxh"
INCLUDE "Spell.oxh"
	
REM Define expire date for time limited software
#define expire_year 2001
#define expire_month 6

CONST KUidAppFrame& = &100041BD
CONST KAppName$ = "psiMind"									REM Stick to one word upto 8 chars
CONST Kcontact$ = "kgolding@cix.co.uk" 			REM Contact details
CONST KcopyRight$ = "© K.Golding 2000-2001"	REM Copyright details
CONST Kprogramversion$ = "0.8 Alpha"				REM Program version
CONST KregKey& = 492203											REM App unique reg key
	
CONST Kzoommax% = 7						REM Maximum levels of zoom
CONST KcmdN$ = "noemtrk"			REM Commands New/Open/Exit/Zoom/Toolbar/Register/Prefs
CONST KcmdS$ = "am"						REM Commands About/Zoom
CONST KblankbitmapWidth% = 5	REM Width of blank bitmap

CONST Kmax_children% = 120			REM Maximum number of children displayed per item
CONST Kuser_children% = 10		REM Maximum number of children the user can add per item
	
APP psiMind, KUidAppFrame&
	FLAGS 1											REM Can create files
	ICON "psiMind.mbm"					REM Icon
	ICON "psiMind.mbm"					REM Icon mask
ENDA

REM #INCLUDE "tests.opl"

PROC Start:
	DEFAULTWIN 1								REM Set default window to 4 colour mode
	gIPRINT "Loading " + KAppName$
	CheckExpired:
	LOADM "Z:\System\OPL\Toolbar.opo"
	tBarLink:("Main")
ENDP
	
PROC Main:
	GLOBAL ev&(16), minit%			REM Current event, last menu item selected
	GLOBAL redraw%							REM Set to false to prevent screen redraws whilst init.
	GLOBAL cmdN$(26), cmdS$(26)	REM command keys allowed
	GLOBAL fullscreenwidth%			REM Static, machines default width
	GLOBAL fullscreenheight%		REM Static, machines default height
	GLOBAL maxwidth%						REM maximum width after toolbar
	GLOBAL exit%								REM Set True to exit program
	GLOBAL fopen%							REM True if data file open
	GLOBAL fname$(255)					REM Current opened file name
	GLOBAL zoomlevel%					REM Current zoom level (1 to Kzoommax%)
	GLOBAL zoomfont&(Kzoommax%)	REM Zoom font ID
	GLOBAL fontheight%					REM Height of current zoom font
	GLOBAL fontdescent%				REM Descent of current zoom font
	GLOBAL blankbitmap&				REM Blank bitmap for toolbar
	GLOBAL prefflag%					REM preferences flag
	
	GLOBAL SpellAlt$(255)			REM For Spell Checker

	GLOBAL username$(30)			REM Registered user name	
	GLOBAL regcode&						REM Registered code
	GLOBAL mainitem&						REM Main/centre item
	GLOBAL markedItem&						REM Last item marked via Cut/Copy
	GLOBAL findtext$(20)				REM User text to search for
	GLOBAL gui_id&(Kmax_children%)	REM gui database of items diplayed
	GLOBAL gui_x1%(Kmax_children%)
	GLOBAL gui_y1%(Kmax_children%)
	GLOBAL gui_x2%(Kmax_children%)
	GLOBAL gui_y2%(Kmax_children%)
	GLOBAL gui_lastid&
	GLOBAL gui_counter%
	GLOBAL ExportFormat%				REM Export as ext format option
	
	Initialize:											REM Setup defaults, read ini settings & draw screen
	
	SystemEvent:(CMD$(3) + CMD$(2))		REM Deal with init system event
	DoSetCmd:												REM Set up available user commands
	
	AppRun:													REM Call event handling loop
	
	WriteIniFile:										REM Save ini file settings
	CloseFile:											REM Close users file
ENDP
	
PROC DrawScreen:
	IF NOT redraw% :RETURN :ENDIF
	gUSE 1														REM Use default screen
	gUPDATE OFF
	gSETWIN 0, 0, MaxWidth%, fullscreenheight%
	gCLS
		
	UpdateToolBar:
	UpdateScreen:
	gUPDATE ON
ENDP
	
PROC UpdateScreen:
	LOCAL background%

	background% = gLOADBIT("z:\system\data\eikon.mbm", 0, 0)
	gUSE 1
	gAT 0, 0
	gPATT background%, gWidth, gHeight, 3
	gBOX gWIDTH, gHEIGHT
	gCLOSE background%
	
	gUSE 1
	
	IF fopen%
		DrawDB:
	ELSE
		rem No file open
	ENDIF
ENDP
	
PROC UpdateToolBar:
	LOCAL off%(6)
	IF fopen%
		PARSE$("*.*", fname$, off%())
		tBarSetTitle:(MID$(fname$, off%(4), 8))
	ELSE
		tBarSetTitle:(KAppName$)
	ENDIF
ENDP
	
PROC AppPrint:								REM Print using print.opx
	BUSY "Printing...", 1, 2
	RESETPRINTING:
	SENDSTRINGTOPRINTER:("This is a test page")
	BUSY OFF
ENDP

PROC CmdL%:											REM Attach File %l
	LOCAL n$(255), d%
	IF NotAllowed%:(%l) :RETURN :ENDIF
	
	Goto_id%:(mainitem&, -1)
	OPEN """" + fname$ + """ SELECT * FROM data WHERE id=" + GEN$(mainitem&, 10) + " AND type='FileLink'", B, id&, type$, data$
	IF Check%:(A.flags%, 1)
		REM Get existing link from option data table
		n$ = B.data$
	ENDIF
	CLOSE
	
	dINIT "Attach/link external file"
	dFILE n$, "File,Folder,Disk", 0
	IF Check%:(A.flags%,1)
		dBUTTONS "Link to item", 13, "Remove link", %r, "Cancel", -27
	ELSE
		dBUTTONS "Link to item", 13, "Cancel", -27
	ENDIF
	d% = DIALOG
	IF d% = %r
		REM Remove flag
		USE A
		MODIFY
		A.flags% = 0
		PUT
		UpdateScreen:
	ELSEIF d% = 13
		REM set flag
		USE A
		MODIFY
		A.flags% = 1
		PUT
		
		REM Add file name to option data table
		OPEN """" + fname$ + """ SELECT * FROM data WHERE id=" + GEN$(mainitem&, 10) + " AND type='FileLink'", B, id&, type$, data$
		USE B
		IF EOF
			INSERT
			B.id& = mainitem&
			B.type$ = "FileLink"
		ELSE
			MODIFY
		ENDIF
		B.data$ = n$
		PUT
		CLOSE

		UpdateScreen:
	ENDIF
ENDP

PROC CmdSL%:										REM File/Info %L
	LOCAL version$(40)
	IF NotAllowed%:(%L) :RETURN :ENDIF
	
	OPEN """" + fname$ + """ SELECT data FROM data WHERE id=0 AND type='ProgVer'", B, data$
	version$ = B.data$
	CLOSE
	
	USE A	:	LAST
	dINIT "File Information"
	dTEXT "Name:", fname$
	dTEXT "Records:", GEN$(COUNT, 10)
	dTEXT "Created by:", version$
	DIALOG
ENDP

PROC CmdE%:										REM File/Exit %e
	exit% = KTrue%
ENDP

PROC CmdF%:										REM Edit/Find %f
	IF NotAllowed%:(%f) :RETURN :ENDIF
	
	dINIT "Find"
	dEDIT findtext$, "Search for", 20
	IF DIALOG
		USE A	:	FIRST
		DoFind:
	ENDIF
ENDP

PROC DoFind:
	IF FIND("*" + findtext$ + "*")
		gui_lastid& = mainitem&
		mainitem& = A.id&
		UpdateScreen:
		gIPRINT "Found - Use Ctrl-J to find next"
	ELSE
		gIPRINT "Not found"
	ENDIF	
ENDP

PROC CmdJ%:										REM Edit/Find Next %j
	IF NotAllowed%:(%j) :RETURN :ENDIF
	Goto_id%:(mainitem&, -1)
	NEXT
	DoFind:
ENDP

PROC CmdN%:										REM File/New %n
	LOCAL n$(255)
	
	n$ = CREATEFILEDIALOG$:(GETDOC$)
	IF n$ <> ""
		CreateFile:(n$)
	ENDIF
ENDP

PROC CmdO%:										REM File/Open %o
	LOCAL n$(255)
	
	n$ = OPENFILEDIALOG$:(GETDOC$, 0, 0, KUidAppFrame&)
	IF n$ <> ""
		OpenFile:(n$)
	ENDIF
ENDP
	
PROC CmdSC%:									REM File/Close %C
	CloseFile:
	DrawScreen:
ENDP

PROC CmdSG%:									REM Save as %G
	LOCAL n$(255)
	IF NotAllowed%:(%G) :RETURN :ENDIF
	n$ = CREATEFILEDIALOG$:(GETDOC$)
	IF n$ <> ""
		REM Save as n$
	ENDIF
ENDP

PROC CmdSJ%:										REM Merge in %J
	LOCAL n$(255), id&, d%
	IF NotAllowed%:(%T) :RETURN :ENDIF

	gIPRINT "Feature not yet supported"
	RETURN	

	n$ = GETDOC$ + ".txt"
	dINIT "Merge file in"
	dFILE n$, "File,Folder,Disk", 1 + 16
	IF mainitem& <> 1
		dTEXT "", "Merge in to"
		dBUTTONS "Current item", %c, "Main item", 13, "Cancel", -27
	ELSE
		dBUTTONS "Merge in", 13, "Cancel", -27
	ENDIF
	d% = DIALOG
	
	id& = 1		
	IF (d% = %c) AND (mainitem& <> 0)
		id& = mainitem&
	ELSEIF d% = 0
		RETURN
	ENDIF
	MergeIn:(n$, id&)	
ENDP
	
PROC CmdS%:										REM %s
	IF NotAllowed%:(%S) :RETURN :ENDIF
ENDP
	
PROC CmdR%:										REM Register %r
RegDialog::
	dINIT "Register"
	dEDIT username$, "User name", 20
	dLONG regcode&, "Registration code", 0, &FFFFFF
	dTEXT "", "", $800
	dTEXT "", "Email kgolding@cix.co.uk for registration details.", 2
	IF DIALOG
		IF CheckRegCode:(username$, regcode&, KregKey&)
			gIPRINT "Thank you for registering!"
		ELSE
			gIPRINT "INVALID REGISTRATION CODE!"
			GOTO RegDialog
		ENDIF
	ENDIF
ENDP
	
PROC CmdK%:										REM Special/Prefernces %k
	LOCAL SpellCheck%
	
	SpellCheck% = GetBit%:(prefflag%, 1)
	dINIT "Preferences"
	dCHECKBOX SpellCheck%, "Automatically spell check items"
	IF DIALOG
		prefflag% = SetBit%:(prefflag%, 1, SpellCheck%)
	ENDIF
ENDP
	
PROC CmdSA%:									REM Special/About %A
	dINIT "About " + KAppName$ + " " + Kprogramversion$, 1 + 8
	dTEXT "", "Simple mind mapping software for the Psion"
	dTEXT "Contact:", "kgolding@cix.co.uk", 2
	dTEXT "", KcopyRight$, $8002
	dBUTTONS "OK", 13
	DIALOG
ENDP

PROC OK2AddChild%:
	LOCAL isreg%, limit%
	
	isreg% = CheckRegCode:(username$, regcode&, KregKey&)
	IF isreg%
		limit% = 10		REM Registered
	ELSE
		limit% = 3		REM Un-Registered
	ENDIF
	
	IF NumberChildren%:(mainitem&) < limit%
		RETURN -1
	ELSE
		dINIT "No more child items allowed"
		dTEXT "","There is a limited of " + GEN$(limit%, 3) + " child items per item"
		IF NOT isreg%
			dTEXT "", "in the unregistered version."
			dTEXT "", "This can be extended to 10 once you register"
			dBUTTONS "Close", 13, "Register", %r
		ENDIF
		IF DIALOG = %r
			CmdR%:
		ENDIF
		RETURN 0
	ENDIF
ENDP

PROC CmdA%:										REM Record/Add %a
	LOCAL newid&
	IF NotAllowed%:(%a) :RETURN :ENDIF
	IF OK2AddChild%:
		USE A	:	LAST
		newid& = A.id& + 1
		INSERT
		A.id& = newid&
		IF newid& = 1
			A.parent& = 0
		ELSE
			A.parent& = mainitem&
		ENDIF
		EditRecord:("")
	ENDIF
ENDP
	
PROC CmdSE%:									REM Record/Edit %E
	IF NotAllowed%:(%E) :RETURN :ENDIF
	Goto_id%:(mainitem&, -1)
	MODIFY
	EditRecord:("")
ENDP
	
PROC CmdSD%:									REM Record/Delete %D
	LOCAL numChildren%
	
	IF NotAllowed%:(%D) :RETURN :ENDIF
	
	IF mainitem& = 1
		gIPRINT "ERROR - You can not delete the root item"
		RETURN
	ENDIF
	
	numChildren% = NumberChildren%:(mainitem&)
	Goto_id%:(mainitem&, -1)
	dINIT "Delete item?"
	dTEXT "", A.title$, 2
	IF numChildren%
		dTEXT "","This will delete all of its child items as well?"
	ENDIF
	dBUTTONS "No", -(%n + $300), "Yes", %y + $300
	
	IF DIALOG = %y
		DeleteAll:(mainitem&)
		mainitem& = gui_id&(1)
		gui_lastid& = 1
		UpdateScreen:
	ENDIF
ENDP
	
PROC CmdSU%:									REM Page setup %U
	PAGESETUPDIALOG:
ENDP
	
PROC CmdSP%:									REM Print setup %P
	PRINTRANGEDIALOG:
ENDP

PROC CmdST%:									REM Export as text %T
	LOCAL n$(255), id&, d%
	IF NotAllowed%:(%T) :RETURN :ENDIF

	n$ = GETDOC$ + ".txt"
	dINIT "Export as text"
	dFILE n$, "File,Folder,Disk", 1 + 16
	dTEXT "", "", $800
	dCHOICE ExportFormat%, "Export format", "Plain,Numbered,Numbered/Letters,Roman"
	IF mainitem& <> 1
		dBUTTONS "Current branch", %c, "Entire file", 13, "Cancel", -27
	ELSE
		dBUTTONS "Export", 13, "Cancel", -27
	ENDIF
	d% = DIALOG
	
	id& = 1		
	IF (d% = %c) AND (mainitem& <> 0)
		id& = mainitem&
	ELSEIF d% = 0
		RETURN
	ENDIF
	ExportAsText:(n$, id&)
ENDP

PROC CmdSV%:									REM Print preview %V
	AppPrint:
	PRINTPREVIEWDIALOG:
ENDP
	
PROC CmdP%:										REM Print %p
	AppPrint:
	ONERR PrintError
	PRINTDIALOG:
	ONERR OFF
	RETURN
	PrintError::
	ONERR OFF
	gIPRINT ERR$(ERR)
ENDP
	
PROC CmdX%:										REM Edit/Cut %x
	IF NotAllowed%:(%x) :RETURN :ENDIF
	gIPRINT "Marked"
	markeditem& = mainitem&
ENDP
	
PROC CmdC%:										REM Edit/Copy %c
	IF NotAllowed%:(%c) :RETURN :ENDIF
	
ENDP
	
PROC CmdV%:										REM Edit/Paste %v
	IF NotAllowed%:(%v) :RETURN :ENDIF
	IF OK2AddChild%:
		MoveItems:(markeditem&, mainitem&)
	ENDIF
ENDP
	
PROC CmdM%:										REM Zoom in %m
	Zoom:(1)
ENDP
	
PROC CmdSM%:									REM Zoom out %M
	Zoom:(-1)
ENDP
	
PROC CmdT%:										REM Toolbar on/off %t
	IF tbVis%
		tBarHide:
		maxwidth% = fullscreenwidth%
	ELSE
		tBarShow:
		maxwidth% = fullscreenwidth% - tbWidth%
	ENDIF
	DrawScreen:
ENDP
	
PROC EditRecord:(keyboard$)					REM User dialog to edit record
	LOCAL title$(40), d%
	local threadid&, temp&

	IF keyboard$ <> ""
		threadid& = GetThreadIDFromAppUid&:(KUidAppFrame&, temp&)
		SendKeyEventToApp&:(threadid&, 0, 4099, KModifierCtrl&, 0, 0)
		title$ = keyboard$
	ELSE
		title$ = A.title$
	ENDIF
EditDialog::
	dINIT "Add/Edit Record", 1
	dEDIT title$, "Title", 30
	dBUTTONS "Cancel", -27, "OK", 13
	d% = DIALOG
	IF d% = 13
		IF title$ = ""
			gIPRINT "The title can not be blank"
			GOTO EditDialog
		ENDIF
		IF GetBit%:(prefflag%, 1)
			title$ = SpellCheck$:(title$)
		ENDIF
		A.title$ = title$
		PUT
		UpdateScreen:
	ELSE
		CANCEL
	ENDIF
ENDP
	
PROC DoSetCmd:								REM Set up user commands
REM This is called at the start of the program, after each call to
REM a "Cmd" procedure, and whenever the data file changes
	cmdN$ = KcmdN$							REM Enable standard commands
	cmdS$ = KcmdS$
	IF fopen%										REM If file is open then allow
		cmdN$ = cmdN$ + "pcxv"		REM Print/Cut/Copy/Paste
		cmdS$ = cmdS$ + "cupvtj"	REM Close/Page setup/Print setup/Print preview/Export as text/Merge in
		
		cmdN$ = cmdN$ + "alf"			REM Add, Find, Attach file
		cmdS$ = cmdS$ + "edl"			REM Edit, Delete, File Info

		IF findtext$ <> ""
			cmdN$ = cmdN$ + "j"			REM Find Next
		ENDIF
	ENDIF
ENDP
	
PROC DoMenu:									REM Display users Menu
	mINIT
	mCASC "Printing", "Page setup...", m%:(%U), "Print setup...", m%:(%P), "Print preview...", m%:(%V), "Print...", m%:(%p)
	mCASC "More", "Export as text...", m%:(%T), "Merge in", m%:(%J)
	mCARD "File", "New...", %n, "Open...", %o, "Close", -m%:(%C), "Info", -m%:(%L), "Printing>", 16, "More>", -16, "Close", %e
	mCARD "Edit", "Cut", m%:(%x), "Copy", m%:(%c), "Paste", -m%:(%v), "Find", m%:(%f), "Find Next", m%:(%j)
	mCARD "View", "Zoom in", %m, "Zoom out", -%M, "Toolbar", %t OR tbMenuSym%
	mCARD "Record", "Add...", m%:(%a), "Edit...", m%:(%E), "Delete...", -m%:(%D), "Attach/link file...", m%:(%l)
	mCARD "Special", "Preferences...", %k, "Register...", %r, "About...", %A
	DoCommandKey:(MENU(minit%))
ENDP
	
PROC KeyPressed:(k%, shift%)	REM Deal with direct (non ctrl) keys
	LOCAL k$(1), newid&
	IF fopen%
		IF (k% = 4098) OR (k% = 32)		REM Home
			gui_lastid& = mainitem&
			mainitem& = 1
			UpdateScreen:
		ELSEIF k% = 27			REM Escape
				IF mainitem& <> gui_lastid&
					newid& = mainitem&
					mainitem& = gui_lastid&
					gui_lastid& = newid&
					UpdateScreen:
				ENDIF
		ELSEIF k% = 8		REM Del
			IF mainitem& > 1
				Goto_id%:(mainitem&, -1)
				gui_lastid& = mainitem&
				mainitem& = A.parent&
				UpdateScreen:
			ELSE
				gIPRINT "You are already at root item!"
			ENDIF
		ELSE
			k$ = CHR$(k%)
			IF (k$ > " ") AND (k% < 128)
				IF OK2AddChild%:
					USE A	:	LAST
					newid& = A.id& + 1
					INSERT
					A.id& = newid&
					A.parent& = mainitem&
					EditRecord:(k$)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDP

PROC PenDown:(wid%, x%, y%)		REM Pen Down
	IF wid% = 1
	ENDIF
ENDP
	
PROC PenUp:(wid%, x%, y%)			REM Pen Up
	LOCAL i%
	
	IF wid% = 1
		i% = 1
		WHILE i% <= gui_counter%
			IF (x% >= gui_x1%(i%)) AND (x% <= gui_x2%(i%))
				IF (y% >= gui_y1%(i%)) AND (y% <= gui_y2%(i%))
					gui_lastid& = mainitem&
					mainitem& = gui_id&(i%)
					UpdateScreen:
					BREAK
				ENDIF
			ENDIF
			i%++
		ENDWH
	ENDIF
ENDP

PROC Squared&:(a%)
	RETURN INT(a%) * INT(a%)
ENDP

PROC PenDrag:(wid%, x%, y%)		REM Pen Dragged
	IF wid% = 1
	ENDIF
ENDP
	
PROC AppGotFocus:							REM Application has got focus
ENDP
	
PROC AppLostFocus:						REM Application has lost focus
ENDP
	
PROC MachineOn:								REM Machine turned on
ENDP
	
PROC DoCutCopyPaste:					REM Sidebar menu cut/copy/paste
	LOCAL ret%
	ret% = mPOPUP(5, 40, 0, "Cut", m%:(%x), "Copy", m%:(%c), "Paste", m%:(%v))
	IF ret% <> 0
		DoCommandKey:(ret%)
	ENDIF
ENDP
	
PROC DoIRTxRx:								REM Sidebar menu infrared send/receive
	LOCAL ret%
	REM Take out the "+ $1000" to enable send/receive
	ret% = mPOPUP(5, 90, 0, "Infrared send", 1 + $1000, "Infrared receive", 2 + $1000)
	IF ret% = 1
		REM Do Infrared Send
	ELSEIF ret% = 2
		REM Do Infrared Recieve
	ENDIF
ENDP

PROC ZoomTo:(i%)
	LOCAL i&(48)
	
	gFONT zoomfont&(i%)
	gINFO32 i&()
	fontheight% = i&(3) + i&(4)		REM Set global total height off font
	fontdescent% = i&(4)					REM Set global descent off font
ENDP

PROC Zoom:(by%)								REM Zoom in/out
	zoomlevel% = zoomlevel% + by%
	IF zoomlevel% < 3
		zoomlevel% = Kzoommax%
	ELSEIF zoomlevel% > Kzoommax%
		zoomlevel% = 3
	ENDIF
	ZoomTo:(zoomlevel%)
	DrawScreen:
ENDP
	
PROC CreateFile:(f$)						REM Create users data file
	LOCAL off%(6)
	CloseFile:
	TRAP DELETE f$
	ONERR ErrCreateFile
	SETDOC f$
	
	REM Create file here
	CREATE """" + f$ + """ FIELDS id,type(12),data(240) TO data", A, id&, type$, data$
	INSERT			REM Created by version
	A.id& = 0
	A.type$ = "ProgVer"
	A.data$ = Kprogramversion$
	PUT
	INSERT			REM default ExportFormat
	A.id& = 0
	A.type$ = "ExFormat"
	A.data$ = "2"	REM Numbered
	PUT

	CLOSE
	
	CREATE """" + f$ + """ FIELDS id,parent,title(40),flags TO items", A, id&, parent&, title$, flags%

	REM Add root item
	PARSE$("*.*", f$, off%())
	INSERT
	A.id& = &1
	A.parent& = &0
	A.title$ = MID$(f$, off%(4), 255)
	PUT
	
	ONERR OFF
	fopen% = KTrue%
	fname$ = f$
	UpdateToolBar:
	UpdateScreen:
	RETURN
ErrCreateFile::
	ONERR OFF
	ALERT("Error creating '"+ f$ + "'", ERR$(ERR) + " (" + GEN$(ERR, 5) + ")")
rem	SETDOC ""
	UpdateToolBar:
	UpdateScreen:
ENDP
	
PROC OpenFile:(f$)							REM Open users data file
	CloseFile:
	ONERR ErrOpenFile

	REM Get any optional data first
	OPEN """" + f$ + """ SELECT * FROM data", A, id&, type$, data$
	IF FIND("ExFormat")
		ExportFormat% = VAL(A.data$)
	ENDIF
	CLOSE
	
	REM Open file here
	OPEN """" + f$ + """ SELECT * FROM items", A, id&, parent&, title$, flags%
	
	ONERR OFF
	SETDOC f$
	fopen% = KTrue%
	fname$ = f$
	
	UpdateToolBar:
	UpdateScreen:
	RETURN
ErrOpenFile::
	ONERR OFF
rem	IF ERR <> KErrInUse%
		ALERT("Error opening '"+ f$ + "'", ERR$(ERR) + " (" + GEN$(ERR, 5) + ")")
rem	ENDIF
	UpdateToolBar:
	UpdateScreen:
ENDP
	
PROC CloseFile:									REM Close file, even if not open!
	IF NOT(fopen%) :RETURN :ENDIF
	USE A
	CLOSE
	
	REM Save any optional data
	OPEN """" + fname$ + """ SELECT * FROM data", A, id&, type$, data$
	IF FINDFIELD("ExFormat", 1, 1, 3)
		MODIFY
	ELSE
		INSERT
		A.type$ = "ExFormat"
		A.id& = 0
	ENDIF
	A.data$ = GEN$(ExportFormat%, 1)
	PUT
	CLOSE

	fopen% = KFalse%
	fname$ = ""
	mainitem& = 0
	gui_lastid& = 0
	UpdateToolBar:
ENDP
	
PROC ReadIniFile:								REM Get ini file settings
	LOCAL name$(255), off%(6)
	name$ = PARSE$(".ini", CMD$(1), off%())
	ONERR ErrReadIniFile
	OPEN """" + name$ + """", I, lastusedfile$, zoomlevel%, tbVis%, username$, regcode&, prefflag%
	fname$ = I.lastusedfile$
	zoomlevel% = I.zoomlevel%
	tbVis% = I.tbVis%
	username$ = I.username$
	regcode& = I.regcode&
	prefflag% = I.prefflag%
	CLOSE
ErrReadIniFile::
	REM Ignore error and don't use ini file settings!
	ONERR OFF
ENDP
	
PROC WriteIniFile:							REM Save ini file settings
	LOCAL name$(255), off%(6)
	
	name$ = PARSE$(".ini", CMD$(1), off%())
	TRAP DELETE name$
	CREATE """" + name$ + """", I, lastusedfile$, zoomlevel%, tbVis%, username$, regcode&, prefflag%
	INSERT
	I.lastusedfile$ = fname$
	I.zoomlevel% = zoomlevel%
	I.tbVis% = tbVis%
	I.username$ = username$
	I.regcode& = regcode&
	I.prefflag% = prefflag%
	PUT
	CLOSE
ENDP

PROC OpenOwnMap:
	LOCAL off%(6), f$(250)
	
	f$ = PARSE$("psiMind Help.A", CMD$(1), off%())
	f$ = LEFT$(f$, LEN(f$) - 2)

	IF EXIST(f$)
		OpenFile:(f$)
		RETURN -1
	ELSE
		RETURN 0
	ENDIF
ENDP

PROC SystemEvent:(s$)						REM Deal with system event
	LOCAL a$(1)
	a$ = LEFT$(s$, 1)
	IF a$ = "C"
		CreateFile:(MID$(s$, 2, 255))
	ELSEIF a$ = "O"
		OpenFile:(MID$(s$, 2, 255))
	ELSEIF a$ = "R"
		IF EXIST(fname$)
			OpenFile:(fname$)
		ELSE
			REM gIPRINT "Could not open last file"
			IF NOT OpenOwnMap:
				CmdN%:											REM Offer to create new
			ENDIF
		ENDIF
	ELSEIF a$ = "X"
		exit% = KTrue%
	ENDIF
ENDP
	
PROC AppRun:											REM The main event handling loop
	LOCAL k%, shift%
	LOCAL w%, x%, y%
	
	DO
		LOCK OFF
		GETEVENT32 ev&()
		IF (ev&(1) = $406) OR (ev&(1) = $407) OR (ev&(1) = $409) OR (ev&(1) = $40A)
			REM Throw away keydown & keyup, pen enter/exit contact
			CONTINUE
		ENDIF
		LOCK  ON
		IF (ev&(1) AND $400) = 0			REM Keyboard event
			k% = ev&(1)
			shift% = (ev&(4) AND 2)
			IF k% = KKeyMenu%
				DoMenu:
			ELSEIF (ev&(4) AND 4) = 4		REM Ctrl key
				DoCommandKey:(96 + k% - shift% * 16)
			ELSE												REM Normal key
				KeyPressed:(k%, shift%)
			ENDIF
		ELSEIF ev&(1) = $408					REM Pen event
			IF tBarOffer%:(ev&(3), ev&(4), ev&(6), ev&(7))
				DoSetCmd:
				CONTINUE
			ELSE
				w% = ev&(3)
				x% = ev&(6)
				y% = ev&(7)
				IF ev&(4) = 0							REM Pen Down
					PenDown:(w%, x%, y%)
				ELSEIF ev&(4) = 1					REM Pen Up
					PenUp:(w%, x%, y%)
				ELSEIF ev&(4) = 6					REM Pen Dragged
					PenDrag:(w%, x%, y%)
				ENDIF
			ENDIF
		ELSEIF ev&(1) = 10000					REM SidebarMenu
			DoMenu:
		ELSEIF ev&(1) = 10001					REM Cut/Copy/Paste
			DoCutCopyPaste:
		ELSEIF ev&(1) = 10002					REM IR Rx/Tx
			DoIRTxRx:
		ELSEIF ev&(1) = 10003					REM Zoom out
			DoCommandKey:(%m)
		ELSEIF ev&(1) = 10004					REM Zoom in
			DoCommandKey:(%M)
		ELSEIF ev&(1) = $401					REM App got focus
			AppGotFocus:
		ELSEIF ev&(1) = $402					REM App lost focus
			AppLostFocus:
		ELSEIF ev&(1) = $403					REM Machine switched on
			MachineOn:
		ELSEIF ev&(1) = $404					REM System event
			SystemEvent:(GETCMD$)
		ELSE 													REM Unknown event
			gIPRINT "Unknown event " + GEN$(ev&(1), 8) + ", " + GEN$(ev&(2), 8) + ", " + GEN$(ev&(3), 8)
		ENDIF
	UNTIL exit%
ENDP
	
PROC DoCommandKey:(k%)						REM Calls PROC's as required
	LOCAL k$(1)
	
	k$ = CHR$(k%)
	IF k$ > "Z"
		IF LOC(cmdN$, k$)
			@%("Cmd" + k$):
			DoSetCmd:
		ENDIF
	ELSE
		IF LOC(cmdS$, k$)
			@%("CmdS" + k$):
			DoSetCmd:
		ENDIF
	ENDIF
ENDP
	
PROC NotAllowed%:(k%)						REM Returns true if command allowed
	IF ((k% <= ASC("Z")) AND LOC(cmdS$, CHR$(k%))) OR ((k% >= ASC("a")) AND LOC(cmdN$, CHR$(k%)))
		RETURN KFalse%
	ELSE
		gIPRINT "This item is not available", KBusyTopRight%
		RETURN KTrue%
	ENDIF
ENDP
	
PROC m%:(k%)										REM Returns menu item dimmed if applicable
	IF ((k% <= ASC("Z")) AND LOC(cmdS$, CHR$(k%))) OR ((k% >= ASC("a")) AND LOC(cmdN$, CHR$(k%)))
		RETURN k%
	ELSE
		RETURN k% OR $1000
	ENDIF
ENDP
	

PROC Initialize:
	SETCOMPUTEMODE:(KComputeModeOff&)	REM Give me speed, I'm a good program
	SETFLAGS &010001									REM Enable machiine on events & auto compact of db
	fopen% = KFalse%									REM No file open
	redraw% = KFalse%									REM Switch off screen drawing whilst init.
	fullscreenwidth% = gWIDTH					REM Before default window changes
	fullscreenheight% = gHEIGHT				REM As above
	mainitem& = 1
	gui_lastid& = 1
	
	prefflag% = $EFFF	REM All preference set to ON
	
	zoomlevel% = 4										REM Set zoom to middle
	zoomfont&(1) = KFontTiny4&				REM Zoom font sizes
	zoomfont&(2) = KFontArialNormal8&
	zoomfont&(3) = KFontArialNormal11&
	zoomfont&(4) = KFontArialNormal13&
	zoomfont&(5) = KFontArialNormal15&
	zoomfont&(6) = KFontArialNormal18&
	zoomfont&(7) = KFontArialNormal22&
	
	IF IsColorMachine%:										REM Create blank bitmap
		blankbitmap& = gCREATEBIT(0, KblankbitmapWidth%, $5)
	ELSE
		blankbitmap& = gCREATEBIT(0, KblankbitmapWidth%, $2)
	ENDIF
	gUSE 1																REM Switch back to screen
	
	tBarInit:(KAppName$, gWIDTH, gHEIGHT)
	tBarButt:("a", 1, "Add" + CHR$(10) + "New", 0, blankbitmap&, blankbitmap&, 0)
	tBarButt:("E", 2, "Edit", 0, blankbitmap&, blankbitmap&, 0)
	tBarButt:("f", 3, "Find", 0, blankbitmap&, blankbitmap&, 0)
	tBarButt:("j", 4, "Find" + CHR$(10) + "Next", 0, blankbitmap&, blankbitmap&, 0)
	
	tbVis% = -1												REM Set toolbar visible by default
	ReadIniFile:											REM Get ini file settings
	
	tbVis% = NOT tbVis%								REM Update toolbar
	CmdT%:
	
	redraw% = KTrue%									REM Switch screen redrawing on
	Zoom:(0)													REM Set font for zoom level	& draw screen
ENDP
	
PROC Check%:(option%, flag%)				REM Returns true if option contains flag
	RETURN (option% OR flag%) = flag%
ENDP
	
PROC CopyStringToBuffer:(s$, buff&)
	LOCAL i%
	
	POKEL buff&, LEN(s$)
	WHILE i% < LEN(s$)
		POKEB buff& + 4 + i%, ASC(MID$(s$, i% + 1, 1))
		i% = i% + 1
	ENDWH
ENDP
	
PROC CopyBufferToString$:(buff&)
	LOCAL i%, ret$(255), retaddr&
	
	IF (PEEKL(buff&) < 0) OR (PEEKL(buff&) > 255)
		RAISE -1
	ENDIF
	WHILE i% < PEEKL(buff&)
		ret$ = ret$ + CHR$(PEEKB(buff& + 4 + i%))
		i% = i% + 1
	ENDWH
	RETURN ret$
ENDP
	
PROC PrintMultiLine:(string$, maxwidth%, maxheight%)
	LOCAL p%, s$(255), x%, y%
	
	x% = gX											REM Store current X, Y
	y% = gY
	s$ = string$
	WHILE (s$ <> "") AND (gY < (y% + maxheight% - fontheight%))
		p% = gPRINTCLIP(s$, maxwidth%)
		s$ = MID$(s$, p% + 1, 255)
		gAT x%, gY + fontheight%
	ENDWH			
ENDP
	
PROC IsColorMachine%:
	local cinfo&(7)
	
	ONERR cerror
	GCOLORINFO cinfo&()
	IF cinfo&(2)									REM number of colours
		RETURN -1 									REM colour screen
	ENDIF
	RETURN 0 											REM no colour
	
	cerror::
	ONERR off
	IF err= -96
		RETURN 0 											REM no colour
	ENDIF
	RAISE ERR
ENDP
	
PROC CheckExpired:
	IF YEAR < expire_year
		RETURN
	ELSEIF YEAR = expire_year
		IF MONTH < expire_month
			RETURN
		ENDIF
	ENDIF
	ALERT("This software has expired", "Contact " + Kcontact$ + " for a later version", "Exit")
	STOP
ENDP

#INCLUDE "gui.opl"
#INCLUDE "data.opl"
#INCLUDE "CheckRegCode.opl"
#INCLUDE "SpellCheck.opl"
#INCLUDE "BitOps.opl"