REM This file was created by AppFrame on Mon 24 Jul 2000 22:25:52
REM AppFrame for the Psion 5, 5mx, 7 and Netbook, by K.Golding (C) 1999-2000
REM Create by Version dated 3/7/00
	
INCLUDE "Const.oph"
INCLUDE "System.oxh"
INCLUDE "Printer.oxh"
INCLUDE "DBase.oxh"
	
REM Define expire date for time limited software
#define expire_year 2000
#define expire_month 12
	
CONST KUidAppFrame& = 10000000
CONST KAppName$ = "Milage"		REM Stick to one word upto 8 chars
CONST Kcontact$ = "kgolding@cix.co.uk" REM Contact details
CONST KcopyRight$ = "© K.Golding 1999-2000"	REM Copyright details
	
CONST Kzoommax% = 5						REM Maximum levels of zoom
CONST KcmdN$ = "noemtk"					REM Commands New/Open/Exit/Zoom/Toolbar/Prefs
CONST KcmdS$ = "am"						REM Commands About/Zoom
CONST KblankbitmapWidth% = 5		REM Width of blank bitmap
	
APP Milage, KUidAppFrame&
	FLAGS 1											REM Can create files
	ICON "Milage.mbm"				REM Icon
	ICON "Milage.mbm"				REM Icon mask
ENDA
	
PROC Start:
	DEFAULTWIN 1								REM Set default window to 4 colour mode
	gIPRINT "Loading " + KAppName$
	CheckExpired:
	LOADM "Z:\System\OPL\Toolbar.opo"
	tBarLink:("Main")
ENDP
	
PROC Main:
	GLOBAL ev&(16), minit%			REM Current event, last menu item selected
	GLOBAL redraw%							REM Set to false to prevent screen redraws whilst init.
	GLOBAL cmdN$(26), cmdS$(26)	REM command keys allowed
	GLOBAL fullscreenwidth%			REM Static, machines default width
	GLOBAL fullscreenheight%		REM Static, machines default height
	GLOBAL maxwidth%						REM maximum width after toolbar
	GLOBAL exit%								REM Set True to exit program
	GLOBAL fopen%							REM True if data file open
	GLOBAL fname$(255)					REM Current opened file name
	GLOBAL zoomlevel%					REM Current zoom level (1 to Kzoommax%)
	GLOBAL zoomfont&(Kzoommax%)	REM Zoom font ID
	GLOBAL fontheight%					REM Height of current zoom font
	GLOBAL fontdescent%				REM Descent of current zoom font
	GLOBAL blankbitmap&				REM Blank bitmap for toolbar
	
	GLOBAL bmtoprow%, numlistrows%
		
	Initialize:											REM Setup defaults, read ini settings & draw screen
	
	SystemEvent:(CMD$(3) + CMD$(2))		REM Deal with init system event
	DoSetCmd:												REM Set up available user commands
	
	AppRun:													REM Call event handling loop
	
	WriteIniFile:										REM Save ini file settings
	CloseFile:												REM Close users file
ENDP
	
PROC DrawScreen:
	LOCAL background%
	IF NOT redraw% :RETURN :ENDIF
	gUSE 1														REM Use default screen
	gUPDATE OFF
	gSETWIN 0, 0, MaxWidth%, fullscreenheight%	
	gCLS
	
	background% = gLOADBIT("z:\system\data\eikon.mbm", 0, 0)
	gUSE 1
	gPATT background%, gWidth, gHeight, 3
	gBOX gWIDTH, gHEIGHT
	gCLOSE background%
	
	gFILL gWIDTH, fontheight%, 1
	gAT 0, fontheight%
	gLINEBY gWIDTH, 0
	
	gAT 10, fontheight% - fontdescent%
	gStyle 1
	gPRINT "Event driven application frame"
	
	gXBORDER 2, $01
		
	UpdateToolBar:
	UpdateScreen:
	gUPDATE ON
ENDP

PROC SetTopRow:(option%)
	LOCAL i%
	
	USE A
	IF option% = 1		REM Last record - END
		LAST		REM Go to last then backwards xxx
		i% = 1
		WHILE i% < numlistrows%
			IF POS > 1
				BACK
			ENDIF
			i% ++
		ENDWH
	ELSEIF option% = 2		REM First record - HOME
		FIRST
	ELSEIF option% = 3		REM Page up
	ELSEIF option% = 4		REM Page down
	ELSEIF option% = 5		REM Scroll up
		GOTOMARK bmtoprow%
		BACK
	ELSEIF option% = 6		REM Scroll down
		GOTOMARK bmtoprow%
		NEXT
	ENDIF
	IF bmtoprow% <> 0
		KILLMARK bmtoprow%
	ENDIF
	bmtoprow% = BOOKMARK
ENDP
	
PROC UpdateScreen:
	LOCAL x%, y%, w%, h%, col%(3), i%, gap%
	
	gUSE 1														REM Use default screen
	gSTYLE 0
	x% = 5
	y% = fontheight% + 10
	w% = gWIDTH - x% - 10
	h% = gHEIGHT - y% - 10
	numlistrows% = h% / fontheight%
	gap% = 10
	col%(1) = gTWIDTH("XXX 99/99/9999")
	col%(3) = gTWIDTH("9999")
	
	col%(2) = w% - col%(1) - col%(3) - (gap% * 4)
	
	gAT x% - 1, y% -1
	gBOX w% + 2, h% + 2
	gAT x%, y%
	gFILL w%, h%, 1

	IF fopen%
		USE A
		LAST
		gIPRINT "There are " + GEN$(COUNT, 5) + " records"

		IF bmtoprow% = 0
			SetTopRow:(1)
		ENDIF
		GOTOMARK bmtoprow%
		i% = 1
		DO
			gAT x% + gap%, y% - fontdescent% + (i% * fontheight%)
			IF NOT EOF
				gPRINTB ShortDateDay$:(A.date&), col%(1), 3
				gMOVE col%(1) + gap%, 0
				gPRINTB A.loc$, col%(2), 2
				gMOVE col%(2) + gap%, 0
				gPRINTB GEN$(A.miles&, 6), col%(3), 1
				NEXT
			ELSE
				gPRINTB "", w% - (gap% * 2), 1
			ENDIF
			i%++
		UNTIL gY > y% + h% - fontheight%
	ENDIF
ENDP
	
PROC UpdateToolBar:
	LOCAL off%(6)
	IF fopen%
		PARSE$("*.*", fname$, off%())
		tBarSetTitle:(MID$(fname$, off%(4), 8))
	ELSE
		tBarSetTitle:(KAppName$)
	ENDIF
ENDP
	
PROC AppPrint:								REM Print using print.opx
	BUSY "Printing...", 1, 2
	RESETPRINTING:
	SENDSTRINGTOPRINTER:("This is a test page")
	BUSY OFF
ENDP
	
PROC CmdE%:										REM File/Exit %e
	exit% = KTrue%
ENDP
	
PROC CmdN%:										REM File/New %n
	LOCAL n$(255)
	
	n$ = CREATEFILEDIALOG$:(GETDOC$)
	IF n$ <> ""
		CreateFile:(n$)
	ENDIF
ENDP
	
PROC CmdO%:										REM File/Open %o
	LOCAL n$(255)
	
	n$ = OPENFILEDIALOG$:(GETDOC$, 0, 0, KUidAppFrame&)
	IF n$ <> ""
		OpenFile:(n$)
	ENDIF
ENDP
	
PROC CmdSC%:									REM File/Close %C
	CloseFile:
	DrawScreen:
ENDP
	
PROC CmdSG%:									REM Save as %G
	LOCAL n$(255)
	IF NotAllowed%:(%G) :RETURN :ENDIF
	n$ = CREATEFILEDIALOG$:(GETDOC$)
	IF n$ <> ""
		REM Save as n$
	ENDIF
ENDP
	
PROC CmdS%:										REM Save %s
	IF NotAllowed%:(%S) :RETURN :ENDIF
	REM Save
ENDP
	
PROC CmdR%:										REM Revert to saved %r
	IF NotAllowed%:(%r) :RETURN :ENDIF
	REM Revert to last save version
ENDP
	
PROC CmdK%:										REM Special/Prefernces
	dINIT "Preferences"
	dTEXT "", "Your preferences here"
	DIALOG
ENDP
	
PROC CmdSA%:									REM Special/About %A
	dINIT "About " + KAppName$, 1 + 8
	dTEXT "Contact:", "kgolding@cix.co.uk", 2
	dTEXT "", KcopyRight$, $8002
	dBUTTONS "OK", 13
	DIALOG
ENDP
	
PROC CmdA%:										REM Record/Add %a
	IF NotAllowed%:(%a) :RETURN :ENDIF
	EditRecord:(Ktrue%)
ENDP
	
PROC CmdSE%:									REM Record/Edit %E
	IF NotAllowed%:(%E) :RETURN :ENDIF
	EditRecord:(Kfalse%)
ENDP
	
PROC CmdSD%:									REM Record/Delete %D
	IF NotAllowed%:(%D) :RETURN :ENDIF
	dINIT "Are you sure?"
	dTEXT "Delete", """" + A.loc$ + """", 2
	dBUTTONS "No", -(%n + $300), "Yes", %y + $300
	IF DIALOG = %y
		ERASE
		IF EOF
			LAST
		ENDIF
		UpdateScreen:
	ENDIF
ENDP
	
PROC CmdSU%:									REM Page setup %U
	PAGESETUPDIALOG:
ENDP
	
PROC CmdSP%:									REM Print setup %P
	PRINTRANGEDIALOG:
ENDP
	
PROC CmdSV%:									REM Print preview %V
	AppPrint:
	PRINTPREVIEWDIALOG:
ENDP
	
PROC CmdP%:										REM Print %p
	AppPrint:
	ONERR PrintError
	PRINTDIALOG:
	ONERR OFF
	RETURN
	PrintError::
	ONERR OFF
	gIPRINT ERR$(ERR)
ENDP
	
PROC CmdX%:										REM Edit/Cut %x
ENDP
	
PROC CmdC%:										REM Edit/Copy %c
ENDP
	
PROC CmdV%:										REM Edit/Paste %v
ENDP
	
PROC CmdM%:										REM Zoom in %m
	Zoom:(1)
ENDP
	
PROC CmdSM%:									REM Zoom out %M
	Zoom:(-1)
ENDP
	
PROC CmdT%:										REM Toolbar on/off %t
	IF tbVis%
		tBarHide:
		maxwidth% = fullscreenwidth%
	ELSE
		tBarShow:
		maxwidth% = fullscreenwidth% - tbWidth%
	ENDIF
	DrawScreen:
ENDP
	
PROC EditRecord:(addnew%)				REM User dialog to edit record
	LOCAL d%, vech%, date&, loc$(80), miles&
	LOCAL maxvech%

	IF addnew%
		USE A
		IF COUNT > 0
			LAST
			vech% = A.vech%
			date& = A.date& + 1
		ELSE
			date& = DAYS(DAY, MONTH, YEAR)
		ENDIF
	ELSE
		vech% = A.vech%
		date& = A.date&
		loc$ = A.loc$
		miles& = A.miles&
	ENDIF
	
	DoDialog::
	dINIT "Add/edit Log entry"
	dDATE date&, "Date", DAYS(1, 1, 2000), DAYS(DAY, MONTH, YEAR)
	dEDIT loc$, "Locations", 20
	dLONG miles&, "Miles travelled", 0, 9999
	
	OPEN """" + fname$ + """ SELECT vech,reg FROM VECH", V, vech%, reg$
	maxvech% = 0
	WHILE NOT EOF
		dCHOICE vech%, "Vehicle", V.reg$ + ",..."
		maxvech% = V.vech%
		NEXT
	ENDWH
	CLOSE
	dCHOICE vech%, "Vehicle", "Add new vehicle"
	
	dBUTTONS "Cancel", -27, "Save", 13
	d% = DIALOG
	IF d% = 13
		REM Error check user input
		IF loc$ = ""
			gIPRINT "What no locations!"
			GOTO DoDialog
		ELSEIF miles& = 0
			gIPRINT "What no milage!"
			GOTO DoDialog
		ENDIF

		IF vech% > maxvech%
			d% = EditVehicle%:(Ktrue%)
			IF d% > 0
				vech% = d%
			ELSE
				GOTO DoDialog
			ENDIF
		ENDIF

		USE A
		BEGINTRANS
		IF addnew%
			INSERT
		ELSE
			MODIFY
		ENDIF
		A.vech% = vech%
		A.date& = date&
		A.loc$ = loc$
		A.miles& = miles&		
		PUT
		COMMITTRANS
		UpdateScreen:
	ELSE
		CANCEL
	ENDIF
ENDP

PROC EditVehicle%:(addnew%)
	LOCAL d%, vech%, reg$(20), notes$(80)
	
	OPEN """" + fname$ + """ SELECT * FROM VECH", V, vech%, reg$, notes$
	IF addnew%
		IF NOT EOF
			LAST
			vech% = A.vech% + 1
		ELSE
			vech% = 1
		ENDIF
	ELSE
		vech% = V.vech%
		reg$ = V.reg$
		notes$ = V.notes$
	ENDIF
	
	dINIT "Add/Edit Vechicle"
	dEDIT reg$, "Reg number", 12
	dEDIT notes$, "Notes", 20
	dBUTTONS "Cancel", -27, "Save", 13
	
	d% = DIALOG
	IF d% = 13
		USE V
		IF addnew%
			INSERT
		ELSE
			MODIFY
		ENDIF
		V.vech% = vech%
		V.reg$ = reg$
		V.notes$ = notes$
		PUT
		CLOSE
		RETURN vech%
	ELSE
		CLOSE
		RETURN 0
	ENDIF
ENDP

PROC DoSetCmd:								REM Set up user commands
REM This is called at the start of the program, after each call to
REM a "Cmd" procedure, and whenever the data file changes
	cmdN$ = KcmdN$							REM Enable standard commands
	cmdS$ = KcmdS$
	IF fopen%										REM If file is open then allow
		cmdN$ = cmdN$ + "srp"			REM Save/Revert to saved/Print
		cmdS$ = cmdS$ + "cgupv"		REM Close/Save as/Page setup/Print setup/Print preview
		
		cmdN$ = cmdN$ + "a"				REM Add
		IF COUNT > 0							REM If there is at least one record then allow
			cmdS$ = cmdS$ + "de"		REM Edit, Delete
		ENDIF			
	ENDIF
ENDP
	
PROC DoMenu:									REM Display users Menu
	mINIT
	mCASC "Printing", "Page setup...", m%:(%U), "Print setup...", m%:(%P), "Print preview...", m%:(%V), "Print...", m%:(%p)
	mCASC "More", "Save as...", m%:(%G), "Save", m%:(%s), "Revert to saved", m%:(%r)
	mCARD "File", "New...", %n, "Open...", %o, "Close", -m%:(%C), "Printing>", 16, "More>", -16, "Close", %e
	mCARD "Edit", "Cut", m%:(%x), "Copy", m%:(%c), "Paste", m%:(%v)
	mCARD "View", "Zoom in", %m, "Zoom out", -%M, "Toobar", %t OR tbMenuSym%
	mCARD "Record", "Add...", m%:(%a), "Edit...", m%:(%E), "Delete...", m%:(%D)
	mCARD "Special", "Preferences...", %k, "About...", %A
	DoCommandKey:(MENU(minit%))
ENDP
	
PROC KeyPressed:(k%, shift%)	REM Deal with direct (non ctrl) keys
	IF k% = 13
		SetTopRow:(5)
		UpdateScreen:
	ENDIF
ENDP
	
PROC PenDown:(wid%, x%, y%)		REM Pen Down
ENDP
	
PROC PenUp:(wid%, x%, y%)			REM Pen Up
ENDP
	
PROC PenDrag:(wid%, x%, y%)		REM Pen Dragged
ENDP
	
PROC AppGotFocus:							REM Application has got focus
ENDP
	
PROC AppLostFocus:						REM Application has lost focus
ENDP
	
PROC MachineOn:								REM Machine turned on
ENDP
	
PROC DoCutCopyPaste:					REM Sidebar menu cut/copy/paste
	LOCAL ret%
	ret% = mPOPUP(5, 40, 0, "Cut", m%:(%x), "Copy", m%:(%c), "Paste", m%:(%v))
	IF ret% <> 0
		DoCommandKey:(ret%)
	ENDIF
ENDP
	
PROC DoIRTxRx:								REM Sidebar menu infrared send/receive
	LOCAL ret%
	REM Take out the "+ $1000" to enable send/receive
	ret% = mPOPUP(5, 90, 0, "Infrared send", 1 + $1000, "Infrared receive", 2 + $1000)
	IF ret% = 1
		REM Do Infrared Send
	ELSEIF ret% = 2
		REM Do Infrared Recieve
	ENDIF
ENDP
	
PROC Zoom:(by%)								REM Zoom in/out
	LOCAL i&(48)
	
	zoomlevel% = zoomlevel% + by%
	IF zoomlevel% < 1
		zoomlevel% = 1
	ELSEIF zoomlevel% > Kzoommax%
		zoomlevel% = Kzoommax%
	ENDIF
	gFONT zoomfont&(zoomlevel%)
	gINFO32 i&()
	fontheight% = i&(3) + i&(4)		REM Set global total height off font
	fontdescent% = i&(4)					REM Set global descent off font
	DrawScreen:
ENDP
	
PROC CreateFile:(f$)						REM Create users data file
	LOCAL key&
	
	CloseFile:
	TRAP DELETE f$
	ONERR ErrCreateFile
	SETDOC f$
	
	REM Create file here
	CREATE """" + f$ + """ FIELDS vech,date,loc(80),miles TO LOG", A, vech%, date&, loc$,miles&
	
	CLOSE
	key& = DBNewKey&:
	DBAddField:(key&, "date", 0)
	DBCreateIndex:("logindex", key&, f$, "LOG")
	DBDeleteKey:(key&)
	
	CREATE """" + f$ + """ FIELDS vech,reg(20),notes(80) TO VECH", B, vech%, reg$, notes$
	CLOSE
	key& = DBNewKey&:
	DBAddField:(key&, "vech", 0)
	DBCreateIndex:("vechindex", key&, f$, "VECH")
	DBDeleteKey:(key&)

	CREATE """" + f$ + """ FIELDS vech,date,start,end,bus,private TO CLAIM", C, vech%, date&, start&, end&, bus&, private&
	CLOSE
	key& = DBNewKey&:
	DBAddField:(key&, "date", 0)
	DBCreateIndex:("logindex", key&, f$, "CLAIM")
	DBDeleteKey:(key&)
	
	ONERR OFF
	
	OpenFile:(f$)

	RETURN
ErrCreateFile::
	ONERR OFF
	ALERT("Error creating '"+ f$ + "'", ERR$(ERR) + " (" + GEN$(ERR, 5) + ")")
rem	SETDOC ""
	UpdateToolBar:
	UpdateScreen:
ENDP
	
PROC OpenFile:(f$)							REM Open users data file
	CloseFile:
	ONERR ErrOpenFile
	
	REM Open file here
	OPEN """" + f$ + """ SELECT * FROM LOG ORDER BY date", A, vech%, date&, loc$,miles&
	
	ONERR OFF
	SETDOC f$
	fopen% = KTrue%
	fname$ = f$
	SetTopRow:(1)
	UpdateToolBar:
	UpdateScreen:
	RETURN
ErrOpenFile::
	ONERR OFF
rem	IF ERR <> KErrInUse%
		ALERT("Error opening '"+ f$ + "'", ERR$(ERR) + " (" + GEN$(ERR, 5) + ")")
rem	ENDIF
	UpdateToolBar:
	UpdateScreen:
ENDP
	
PROC CloseFile:									REM Close file, even if not open!
	IF NOT(fopen%) :RETURN :ENDIF
	USE A
	CLOSE
	fopen% = KFalse%
	fname$ = ""
	UpdateToolBar:
ENDP
	
PROC ReadIniFile:								REM Get ini file settings
	LOCAL name$(255), off%(6)
	name$ = PARSE$(".ini", CMD$(1), off%())
	ONERR ErrReadIniFile
	OPEN """" + name$ + """", I, lastusedfile$, zoomlevel%, tbVis%
	fname$ = I.lastusedfile$
	zoomlevel% = I.zoomlevel%
	tbVis% = I.tbVis%
	CLOSE
ErrReadIniFile::
	REM Ignore error and don't use ini file settings!
	ONERR OFF
ENDP
	
PROC WriteIniFile:							REM Save ini file settings
	LOCAL name$(255), off%(6)
	
	name$ = PARSE$(".ini", CMD$(1), off%())
	TRAP DELETE name$
	CREATE """" + name$ + """", I, lastusedfile$, zoomlevel%, tbVis%
	INSERT
	I.lastusedfile$ = fname$
	I.zoomlevel% = zoomlevel%
	I.tbVis% = tbVis%
	PUT
	CLOSE
ENDP
	
PROC SystemEvent:(s$)						REM Deal with system event
	LOCAL a$(1)
	a$ = LEFT$(s$, 1)
	IF a$ = "C"
		CreateFile:(MID$(s$, 2, 255))
	ELSEIF a$ = "O"
		OpenFile:(MID$(s$, 2, 255))
	ELSEIF a$ = "R"
		IF EXIST(fname$)
			OpenFile:(fname$)
		ELSE
			gIPRINT "Could not open last file"
			CmdN%:											REM Offer to create new
		ENDIF
	ELSEIF a$ = "X"
		exit% = KTrue%
	ENDIF
ENDP
	
PROC AppRun:											REM The main event handling loop
	LOCAL k%, shift%
	LOCAL w%, x%, y%
	
	DO
		LOCK OFF
		GETEVENT32 ev&()
		IF (ev&(1) = $406) OR (ev&(1) = $407) OR (ev&(1) = $409) OR (ev&(1) = $40A)
			REM Throw away keydown & keyup, pen enter/exit contact
			CONTINUE
		ENDIF
		LOCK  ON
		IF (ev&(1) AND $400) = 0			REM Keyboard event
			k% = ev&(1)
			shift% = (ev&(4) AND 2)
			IF k% = KKeyMenu%
				DoMenu:
			ELSEIF (ev&(4) AND 4) = 4		REM Ctrl key
				DoCommandKey:(96 + k% - shift% * 16)
			ELSE												REM Normal key
				KeyPressed:(k%, shift%)
			ENDIF
		ELSEIF ev&(1) = $408					REM Pen event
			IF tBarOffer%:(ev&(3), ev&(4), ev&(6), ev&(7))
				DoSetCmd:
				CONTINUE
			ELSE
				w% = ev&(3)
				x% = ev&(6)
				y% = ev&(7)
				IF ev&(4) = 0							REM Pen Down
					PenDown:(w%, x%, y%)
				ELSEIF ev&(4) = 1					REM Pen Up
					PenUp:(w%, x%, y%)
				ELSEIF ev&(4) = 6					REM Pen Dragged
					PenDrag:(w%, x%, y%)
				ENDIF
			ENDIF
		ELSEIF ev&(1) = 10000					REM SidebarMenu
			DoMenu:
		ELSEIF ev&(1) = 10001					REM Cut/Copy/Paste
			DoCutCopyPaste:
		ELSEIF ev&(1) = 10002					REM IR Rx/Tx
			DoIRTxRx:
		ELSEIF ev&(1) = 10003					REM Zoom out
			DoCommandKey:(%m)
		ELSEIF ev&(1) = 10004					REM Zoom in
			DoCommandKey:(%M)
		ELSEIF ev&(1) = $401					REM App got focus
			AppGotFocus:
		ELSEIF ev&(1) = $402					REM App lost focus
			AppLostFocus:
		ELSEIF ev&(1) = $403					REM Machine switched on
			MachineOn:
		ELSEIF ev&(1) = $404					REM System event
			SystemEvent:(GETCMD$)
		ELSE 													REM Unknown event
			gIPRINT "Unknown event " + GEN$(ev&(1), 8) + ", " + GEN$(ev&(2), 8) + ", " + GEN$(ev&(3), 8)
		ENDIF
	UNTIL exit%
ENDP
	
PROC DoCommandKey:(k%)						REM Calls PROC's as required
	LOCAL k$(1)
	
	k$ = CHR$(k%)
	IF k$ > "Z"
		IF LOC(cmdN$, k$)
			@%("Cmd" + k$):
			DoSetCmd:
		ENDIF
	ELSE
		IF LOC(cmdS$, k$)
			@%("CmdS" + k$):
			DoSetCmd:
		ENDIF
	ENDIF
ENDP
	
PROC NotAllowed%:(k%)						REM Returns true if command allowed
	IF ((k% <= ASC("Z")) AND LOC(cmdS$, CHR$(k%))) OR ((k% >= ASC("a")) AND LOC(cmdN$, CHR$(k%)))
		RETURN KFalse%
	ELSE
		gIPRINT "This item is not available", KBusyTopRight%
		RETURN KTrue%
	ENDIF
ENDP
	
PROC m%:(k%)										REM Returns menu item dimmed if applicable
	IF ((k% <= ASC("Z")) AND LOC(cmdS$, CHR$(k%))) OR ((k% >= ASC("a")) AND LOC(cmdN$, CHR$(k%)))
		RETURN k%
	ELSE
		RETURN k% OR $1000
	ENDIF
ENDP
	
PROC Initialize:
	SETCOMPUTEMODE:(KComputeModeOff&)	REM Give me speed, I'm a good program
	SETFLAGS &010001									REM Enable machiine on events & auto compact of db
	fopen% = KFalse%									REM No file open
	redraw% = KFalse%									REM Switch off screen drawing whilst init.
	fullscreenwidth% = gWIDTH					REM Before default window changes
	fullscreenheight% = gHEIGHT				REM As above
	
	zoomlevel% = 3										REM Set zoom to middle
	zoomfont&(1) = KFontArialNormal8&	REM Zoom font sizes
	zoomfont&(2) = KFontArialNormal11&
	zoomfont&(3) = KFontArialNormal13&
	zoomfont&(4) = KFontArialNormal15&
	zoomfont&(5) = KFontArialNormal18&
	
	
	IF IsColorMachine%:										REM Create blank bitmap
		blankbitmap& = gCREATEBIT(0, KblankbitmapWidth%, $5)
	ELSE
		blankbitmap& = gCREATEBIT(0, KblankbitmapWidth%, $2)
	ENDIF
	gUSE 1																REM Switch back to screen
	
	tBarInit:(KAppName$, gWIDTH, gHEIGHT)
	tBarButt:("a", 1, "Add new" + CHR$(10) + "entry", 0, blankbitmap&, blankbitmap&, 0)
	tBarButt:("E", 2, "Edit entry", 0, blankbitmap&, blankbitmap&, 0)
	tBarButt:("A", 3, "About", 0, blankbitmap&, blankbitmap&, 0)
	tBarButt:("e", 4, "Exit", 0, blankbitmap&, blankbitmap&, 0)
	
	tbVis% = -1												REM Set toolbar visible by default
	ReadIniFile:											REM Get ini file settings
	
	tbVis% = NOT tbVis%								REM Update toolbar
	CmdT%:
	
	redraw% = KTrue%									REM Switch screen redrawing on
	Zoom:(0)													REM Set font for zoom level	& draw screen
ENDP
	
PROC Check%:(option%, flag%)				REM Returns true if option contains flag
	RETURN (option% OR flag%) = flag%
ENDP
	
PROC CopyStringToBuffer:(s$, buff&)
	LOCAL i%
	
	POKEL buff&, LEN(s$)
	WHILE i% < LEN(s$)
		POKEB buff& + 4 + i%, ASC(MID$(s$, i% + 1, 1))
		i% = i% + 1
	ENDWH
ENDP
	
PROC CopyBufferToString$:(buff&)
	LOCAL i%, ret$(255), retaddr&
	
	IF (PEEKL(buff&) < 0) OR (PEEKL(buff&) > 255)
		RAISE -1
	ENDIF
	WHILE i% < PEEKL(buff&)
		ret$ = ret$ + CHR$(PEEKB(buff& + 4 + i%))
		i% = i% + 1
	ENDWH
	RETURN ret$
ENDP
	
PROC PrintMultiLine:(string$, maxwidth%, maxheight%)
	LOCAL p%, s$(255), x%, y%
	
	x% = gX											REM Store current X, Y
	y% = gY
	s$ = string$
	WHILE (s$ <> "") AND (gY < (y% + maxheight% - fontheight%))
		p% = gPRINTCLIP(s$, maxwidth%)
		s$ = MID$(s$, p% + 1, 255)
		gAT x%, gY + fontheight%
	ENDWH			
ENDP
	
PROC IsColorMachine%:
	local cinfo&(7)
	
	ONERR cerror
	GCOLORINFO cinfo&()
	IF cinfo&(2)									REM number of colours
		RETURN -1 									REM colour screen
	ENDIF
	RETURN 0 											REM no colour
	
	cerror::
	ONERR off
	IF err= -96
		RETURN 0 											REM no colour
	ENDIF
	RAISE ERR
ENDP
	
PROC CheckExpired:
	if year >= expire_year
		if month >= expire_month
			alert("This software has expired", "Contact " + Kcontact$ + " for a later version", "Exit")
			STOP
		endif
	endif
ENDP

PROC ShortDateDay$:(date&)
	LOCAL d%, m%, y%, d$(10)
	
	DAYSTODATE date&, y%, m%, d%
	d$ = DAYNAME$(DOW(d%, m%, y%))
	RETURN d$ + " " + GEN$(d%, 2) + "/" + GEN$(m%, 2) + "/" + GEN$(y%, 4)
ENDP

PROC ShortDate$:(date&)
	LOCAL d%, m%, y%
	
	DAYSTODATE date&, y%, m%, d%
	RETURN GEN$(d%, 2) + "/" + GEN$(m%, 2) + "/" + GEN$(y%, 4)
ENDP

/*
PROC CreateTestData:(num%)
	LOCAL i%
	
	BEGINTRANS
	WHILE i% <= num%
		INSERT
		A.date& = DAYS(1, 1, 2000) + i%
		A.loc$ = "Test data entry"
		A.miles& = i%
		PUT
		i%++
	ENDWH
	COMMITTRANS
ENDP
*/